; ============================================================
; Main entry script
; ------------------------------------------------------------
; 目的:
;   - 個人用 AutoHotkey v2 環境の統合エントリポイント
;   - 各種モジュール（IME制御 / Vim操作 / スニペットUI 等）を
;     一元的に読み込み、共通設定を初期化する
;
; 特徴:
;   - 機能単位で modules / ui に分離
;   - 本ファイルは「配線（glue）」のみを担当し、
;     個別ロジックは持たない
;
; 対象:
;   - AutoHotkey v2.0 以降
;
; ============================================================

#Requires AutoHotkey v2.0
#SingleInstance Force   ; 二重起動防止
#Warn                   ; 潜在的な問題を警告
SetWorkingDir A_ScriptDir  ; 相対パス基準をスクリプト配置先に固定

; ------------------------------------------------------------
; 修飾キーの定義 (Define Modifier Key)
; ------------------------------------------------------------
; 記述例:
;   - "vk1D"      : 無変換 (JISキーボード推奨)
;   - "vk1C"      : 変換
;   - "CapsLock"  : CapsLock
;   - "RAlt"      : 右Alt (USキーボード等で親指位置にある場合)
;   - "AppsKey"   : メニューキー
;   - "Space"     : スペースキー (※長押し判定等の微調整が必要な場合あり)
; ------------------------------------------------------------
MOD_KEY := "vk1D" ; デフォルト：無変換キー

; ------------------------------------------------------------
; Global hotkeys (maintenance)
; ------------------------------------------------------------
^!r:: Reload     ; Ctrl + Alt + R : スクリプト再読み込み
^!q:: ExitApp    ; Ctrl + Alt + Q : スクリプト終了

; ------------------------------------------------------------
; Explorer / Tablacus 関連
; ------------------------------------------------------------
; モジュールの読み込み
#Include modules\FolderToggle.ahk

commonIni := StrReplace(A_ScriptFullPath, ".ahk", ".ini")
FolderToggle.Init(commonIni)
MButton:: FolderToggle.Execute()

; ------------------------------------------------------------
; IME 操作の定義 関連
; ------------------------------------------------------------
#Include "modules\ImeControl.ahk"                  ; IME ON/OFF 制御
; --- IME 操作の定義 ---
; 1. 変換(vk1C) 単押し：IME ON
vk1C:: ImeControl.Toggle(true)

; ------------------------------------------------------------
; Excel風の日付入力の設定
; ------------------------------------------------------------
#Include "modules\DateTimeInsert.ahk" ; Excel 風の日付時刻入力
; 日付入力 (Ctrl + ;) のみ定義
^;:: DateTimeInsert.Execute()

; ------------------------------------------------------------
;  Vim 風ナビゲーション定義
; ------------------------------------------------------------
#Include "modules\VimNavigation.ahk"                    ; Vim 風キーバインド

; ============================================================
; 【修飾キー制御】選択可能な2つのモード
; ============================================================
#Include "modules\ModifierKeyHandler.ahk"

; ------------------------------------------------------------
; モード1: 空打ちでIME切り替え + キー無効化（デフォルト）
; ------------------------------------------------------------
ModifierKeyHandler.Init("vk1D", "sc07B")  ; 無変換キー (Muhenkan)
ModifierKeyHandler.OnTap := (*) => ImeControl.Toggle(false)

; ------------------------------------------------------------
; モード2: キー無効化のみ（IME切り替えなし）
; 上記をコメントアウトし、以下を有効化:
; ------------------------------------------------------------
; ModifierKeyHandler.Init("vk1D", "sc07B")
; ; OnTapを設定しない = キー無効化のみ

; ------------------------------------------------------------
; 他のキーに変更する例:
; ------------------------------------------------------------
; CapsLock:  ModifierKeyHandler.Init("vkF0", "sc03A")
; Space:     ModifierKeyHandler.Init("vk20", "sc039")
; Right Alt: ModifierKeyHandler.Init("vkA5", "sc138")
; ============================================================

#HotIf GetKeyState(MOD_KEY, "P")

; hjkl: 基本移動
*h:: VimNavigation.Move("{Left}", "+{Left}")
*j:: VimNavigation.Move("{Down}", "+{Down}")
*k:: VimNavigation.Move("{Up}", "+{Up}")
*l:: VimNavigation.Move("{Right}", "+{Right}")

; w/b: 単語単位移動
*w:: VimNavigation.Move("^{Right}", "+^{Right}")
*b:: VimNavigation.Move("^{Left}", "+^{Left}")

; 0/4: 行頭・行末移動
*0:: VimNavigation.Move("{Home}", "+{Home}")
*4:: VimNavigation.Move("{End}", "+{End}")

; u/d: ページアップ・ダウン
*u:: VimNavigation.Move("{PgUp}", "+{PgUp}")
*d:: VimNavigation.Move("{PgDn}", "+{PgDn}")

; x: 削除
; *x:: Send("{Del}")
*x:: VimNavigation.Move("{Del}", "{BackSpace}")

; Vimの o / O (行の挿入)
*o:: VimNavigation.OpenLine(GetKeyState("Shift", "P"))

#HotIf

; ------------------------------------------------------------
; 選択テキスト操作系
; ------------------------------------------------------------
#Include modules\WrapPalette.ahk

#HotIf GetKeyState(MOD_KEY, "P")
r:: WrapPalette.Execute()
#HotIf

; ------------------------------------------------------------
; Core behavior modules
; ------------------------------------------------------------
#Include "modules\Hotstrings.ahk"                  ; 定型文・スニペット展開
#Include "tests\TestScript.ahk"                	   ; 検証用（通常は無効）

; ------------------------------------------------------------
; UI components
; ------------------------------------------------------------

; ------------------------------------------------------------
;  Navi ショートカット定義
; ------------------------------------------------------------
#Include "ui\Navi.ahk"      ; ナビゲーション / ランチャ UI

#HotIf GetKeyState(MOD_KEY, "P")
; Navi: GUI 起動
f:: Navi.Show()

; Navi: アクティブ窓のパスを別ツールで開く
v:: Navi.Execute("v") ; VS Code
t:: Navi.Execute("t") ; Tablacus (INIのTE_Pathに依存)
c:: Navi.Execute("c") ; CMD
#HotIf

; リロード: Ctrl + R
^r:: Reload()

; ------------------------------------------------------------
;  スニペット選択 UI
; ------------------------------------------------------------
; 1. インクルード（ここで SnippetPicker クラスを読み込む）
#Include "ui\SnippetPicker.ahk"

; 2. 初期化（クラスが定義された後に実行）
SnippetPicker.Init()

; 3. ホットキー登録
#HotIf GetKeyState(MOD_KEY, "P")
p:: SnippetPicker.Show()
#HotIf

; ------------------------------------------------------------
;  テンポラリメモ
; ------------------------------------------------------------
#Include "ui\TempMemo.ahk"

; 初期化
TempMemo.Init()

; 無変換 + m でテンポラリメモを呼び出し
#HotIf GetKeyState(MOD_KEY, "P")
m:: TempMemo.Toggle()
#HotIf

#Include ui\TransWindow.ahk

; スクリプト起動時にこれを実行させる必要があります
TransWindow.Init()

; ホットキーの設定
vk1D & t:: TransWindow.Show()

#Include modules\ExcelControl.ahk
; 「修飾キーが押されている」かつ「Excelがアクティブ」な場合のみ動作
#HotIf GetKeyState(MOD_KEY, "P") and WinActive("ahk_class XLMAIN")
e:: ExcelControl.SetFontColorRed()
q:: ExcelControl.SetFontColorBlack()
s:: ExcelControl.SetFontColorStrikethrough()
#HotIf